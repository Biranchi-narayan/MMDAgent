###################################################################
#
# CMU GraphQA Receptionist Specific segments
#
#
#
# Written by Shane Smith
#
#
#
###################################################################

# Queries
[InfoQuery]
	(*START *the [Info] *of [SpecNode])
	(*START [SpecNode] *'s [Info])
START
	(what *[Linking])
	(what *'s )
	(i WANT)
	(*can *you FIND *me)
FIND
	(find)
	(get)
	(tell)
	(search for)
WANT
	(want)
	(need)
	(require)
	
;

[NodeQuery]
	(*WHWORD [GenNode] *[AllLinking] *RELVERBPHRASE [SpecNode])
	(*WHWORD [GenNode] *[AllLinking] [SpecNode] *RELVERBPHRASE)
	(*WHWORD *[AllLinking] [SpecNode] *'s [GenNode])
WHWORD
	(what)
	(which)
	(who)
	(when)
	(where)
RELVERBPHRASE
	([RelVerb] *by)
	([RelVerb] *[Prep])
;

[PersonQuery]
	(who [Linking] [SpecNode])
	(who [Linking] [SpecNode] *'s [GenPerson])
;

[LocationQuery]
	(where *[Linking] [SpecLocation])
	(where *[Linking] [SpecNode] 's [GenLocation])
	(where *[_do] [SpecNode] [LocationVerb])
;

# Verbs ------------------------------------------
[Linking]
	(is)
	(are)
	(was)
	(were)
	(be)
	(being)
	(been)
	(am)
;

[_do]
	(do)
	(does)
	(did)
;

[AllLinking]
	(may)
	(might)
	(must)
	(should)
	(could)
	(would)
	(have)
	(had)
	(has)
	(will)
	(can)
	(shall)
	([Linking])
	([_do])
;

[RelVerb]
	([PersonVerb])
	([LocationVerb])
;

[PersonVerb]
	([_advise])
	([_teach])
;

[LocationVerb]
	(sit)
	(sits)
	(work)
	(works)
	(play)
;

# Verbs -----------------
[_advise]
	(advises)
	(advise)
	(advised)
	(advising)
	(mentors)
	(mentor)
	(mentoring)
;
[_teach]
	(teaches)
	(teach)
	(taught)
	(teaching)
;


# Nodes ------------------------------------------
[GenNode]
	([GenLocation])
	([GenPerson])
	([GenCourse])
	([Info])
;

[SpecNode]
	([SpecLocation])
	([SpecPerson])
	([CourseNumber])
;

[GenPerson]
	(PROFESSOR)
	(STUDENT)
	(STAFF)
	(PERSON)
PROFESSOR
	(professor)
	(professors)
	(prof)
	(doctor)
	(dr)
STUDENT
	(student)
	(students)
	(t a)
STAFF
	(staff)
	(employee)
	(employees)
PERSON
	(person)
	(people)
	(he)
	(her)
;

[SpecPerson]
	(*doctor [SpecProfessor])
	(*professor [SpecProfessor])
	([SpecStaff])
	([SpecStudent])
	([_you])
	([_me])
	([_pronoun])
;

[GenLocation]
	(OFFICE)
	([_building])
OFFICE
	(office)
;

[SpecLocation]
	([SpecBuilding])
	([SpecRoom])
;

[GenCourse]
	(course)
	(courses)
	(class)
	(classes)
;


# Info -------------------------------------------
[Info]
	([_phone])
	([_email])
	([_department])
	([_publication])
	([_research])
	([_name])
	([_firstName])
	([_lastName])
	([_fax])
;

# Specifics --------------------------------------
[SpecStaff]
	(abele nicholas)
	(nicholas abele)
	(abele)
	(nicholas)
;

[SpecStudent]
	(smith shane)
	(shane smith)
	(shane)
	(smith)

	(montgomery jack)
	(jack montgomery)
	(jack)
	(montgomery)
;

[_pronoun]
	(he)
	(his)
	(she)
	(her)
	(it)
	(its)
	(they)
	(them)
	(their)
;

# SpecProf net now in SpecProfessor.gra
# [SpecProfessor]

# SpecCourse net now in SpecCourse.gra
# [SpecCourse]

[SpecBuilding]
	(GATES)
	(NSH)
	(HILLMAN)
GATES
	(gates *hillman center for computer science)
	(gates *hillman *[_building])
	(g h c *building)
NSH
	(newell simon *[_building])
	(newell simon hall)
	(n s h *[_building])
HILLMAN
	(hillman center for future generation technologies)
	(hillman *[_building])
;

[SpecRoom]
	(RASHEED)
	(VONAHN)
RASHEED
	(rasheed [_room])
VONAHN
	(von ahn *awesome [_room])
	(aweseome [_room])
;

# Kade Queries -----------------------------------
[KadeQuery]
	([WhoMadeQuery])
	([WhoAreYouQuery])
	([WhatAreYouQuery])
	([CapabilityQuery])
	([AgeQuery])
	([WhenBornQuery])
	([WhereAmI])
	([WhereAreYouFromQuery])
	([TellAJoke])
	([WhosThere])
	([Hello])
	([Goodbye])
	([ThankYou])
	([FavoriteColorQuery])
	([FavoriteFoodQuery])
;

[WhoMadeQuery]
	(WHWORD MADE you)
	(where did you come from)
WHWORD
	(what)
	(who)
MADE
	(made)
	(built)
	(created)
;

[WhoAreYouQuery]
	(who are you)
;

[WhatAreYouQuery]
	(what are you)
;

[CapabilityQuery]
	(what can you do)
	(what do you do)
	(what are you capable of)
	(what can i ask you)
	(what do you know)
;

[AgeQuery]
	(how old are you)
;

[WhenBornQuery]
	(when were you born)
;

[WhereAmI]
	(where am i)
	(where is this)
;

[WhereAreYouFromQuery]
	(*where *are you from)
;

[TellAJoke]
	(tell *me *a joke)
	(*do *you *know *any jokes)
;

[WhosThere]
	(who *'s there)
;

[Hello]
	(hello *there)
	(hi *there)
	(good *TOD)
TOD
	(morning)
	(afternoon)
	(evening)
	(night)
;

[Goodbye]
	(goodbye)
	(see ya)
	(bye *bye)
;

[ThankYou]
	(thank *you)
	(thanks)
;

[FavoriteColorQuery]
	(what *IS your favorite color)
IS
	(is)
	('s)
;

[FavoriteFoodQuery]
	(what *IS your favorite food)
IS
	(is)
	('s)
;

# Terminals --------------------------------------
[_building]
	(building)
	(center)
	(hall)
;

[_room]
	(room)
	(classroom)
	(auditorium)
;

[Prep]
	(in)
	(to)
	(on)
	(of)
	(by)
;

[_you]
	(you)
	(your)
;

[_me]
	(me)
	(i)
;

[_phone]
	(phone)
	(*phone number)
	(telephone *number)
;
[_fax]
	(fax *number)
;
[_email]
	(email *address)
;
[_department]
	(department)
;
[_publication]
	(publication)
	(publications)
	(published *works *papers)
;
[_research]
	(research)
;
[_name]
	(name)
;
[_firstName]
	(first name)
;
[_lastName]
	(last name)
;
